configurations {
    testImplementation.extendsFrom(compileOnlyApi)
}

dependencies {
    compileOnlyApi 'org.sonarsource.api.plugin:sonar-plugin-api'
    implementation 'commons-io:commons-io'
    implementation group: 'org.commonmark', name: 'commonmark', version: '0.21.0'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'



    testImplementation 'junit:junit'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation project(':sonar-plugin-api-impl')
}

jar {
    manifest {
        attributes(
                'Plugin-Key': 'CodeQL',
                'Plugin-Version': project.version,
                'Plugin-Class': 'org.sonar.plugins.codeql.CodeQLPlugin',
                'Plugin-ChildFirstClassLoader': 'false',
                'Sonar-Version': project.version,
                'SonarLint-Supported': 'true',
                'Plugin-Name': 'CodeQL'
        )
    }
    into('META-INF/lib') {
        from configurations.runtimeClasspath
    }
}

artifactoryPublish.skip = false

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            if (release) {
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
